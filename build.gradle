apply plugin: 'groovy'
apply plugin: 'application'

mainClassName = 'nfjs.spring.groovy_demo'

repositories {
    jcenter()
}

ext.db = file("$buildDir/baseball.h2.db")

task generateTestDb(dependsOn:classes, type:JavaExec) {
    onlyIf { !db.exists() }
    main = 'nfjs.geocoder.populate_stadium_data'
    classpath sourceSets.main.runtimeClasspath
}

compileTestGroovy.dependsOn generateTestDb

ext.springVersion = '5.2.8.RELEASE'
ext.spockVersion = '2.0-groovy-3.0'

dependencies {
    implementation 'org.codehaus.groovy:groovy:3.0.5'
    implementation 'org.codehaus.groovy:groovy-swing:3.0.5'
    implementation 'org.codehaus.groovy:groovy-datetime:3.0.5'
    implementation 'org.codehaus.groovy:groovy-json:3.0.5'
    implementation 'org.codehaus.groovy:groovy-sql:3.0.5'
    implementation 'org.codehaus.groovy:groovy-xml:3.0.5'
    implementation "org.springframework:spring-context:$springVersion"
    implementation "org.springframework:spring-jdbc:$springVersion"

    testImplementation 'org.codehaus.groovy:groovy-test:3.0.5'
    testImplementation 'org.codehaus.groovy:groovy-test-junit5:3.0.5'
    testImplementation "org.springframework:spring-test:$springVersion"
    testImplementation platform('org.spockframework:spock-bom:2.0-M3-groovy-3.0')
    testImplementation 'org.spockframework:spock-core'
    testImplementation 'org.spockframework:spock-spring'

    runtimeOnly 'com.h2database:h2:1.3.175'
}

test {
    useJUnitPlatform()
}